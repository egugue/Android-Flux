apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"
    defaultConfig {
        applicationId "example.duane0728.android_flux"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    // https://developer.android.com/topic/libraries/support-library/revisions.html
    def supportLibVer = '26.0.0-beta2'
    implementation "com.android.support:appcompat-v7:$supportLibVer"
    implementation "com.android.support:recyclerview-v7:$supportLibVer"
    implementation "com.android.support:design:$supportLibVer"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    // https://developer.android.com/topic/libraries/architecture/release-notes.html
    def aacVer = "1.0.0-alpha4"
    implementation "android.arch.lifecycle:runtime:$aacVer"
    implementation "android.arch.lifecycle:extensions:$aacVer"
    kapt "android.arch.lifecycle:compiler:$aacVer"

    // RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.1.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    implementation 'com.jakewharton.rxbinding2:rxbinding-recyclerview-v7-kotlin:2.0.0'
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.0.0'

    // Dagger
    implementation 'com.google.dagger:dagger:2.11'
    kapt 'com.google.dagger:dagger-compiler:2.11'

    // Network
    implementation 'com.squareup.okhttp3:okhttp:3.8.1'

    def moshiVer = '1.5.0'
    implementation "com.squareup.moshi:moshi:$moshiVer"
    implementation "com.squareup.moshi:moshi-kotlin:$moshiVer"

    def retrofitVer = '2.3.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVer"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVer"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVer"

    // View
    def butterknifeVer = "8.6.0"
    implementation "com.jakewharton:butterknife:$butterknifeVer"
    kapt "com.jakewharton:butterknife-compiler:$butterknifeVer"

    implementation 'com.github.bumptech.glide:glide:4.0.0-RC1'

    // Tools
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation ('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
